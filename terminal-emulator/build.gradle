apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion project.properties.compileSdkVersion.toInteger()
    ndkVersion project.properties.ndkVersion

    defaultConfig {
        minSdkVersion project.properties.minSdkVersion.toInteger()
        targetSdkVersion project.properties.targetSdkVersion.toInteger()
        multiDexEnabled true

        externalNativeBuild {
            ndkBuild {
                cFlags "-std=c11", "-Wall", "-Wextra", "-Werror", "-fuse-ld=lld",  "-O3", "-fno-stack-protector", "-Wl,--gc-sections"
            }
        }

        ndk {
            abiFilters 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "src/main/jni/Android.mk"
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }
}

tasks.withType(Test) {
    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.1'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.9'
}


publishing {
    publications {
        bar(MavenPublication) {
            groupId 'com.termux'
            artifactId 'terminal-emulator'
            version '0.106.1'
            artifact("$buildDir/outputs/aar/terminal-emulator-release.aar")
        }
    }
 
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/najahiiii/termux-app")

            credentials {
                username = System.getenv("GH_USERNAME")
                password = System.getenv("GH_TOKEN")
            }
        }
    }
}
